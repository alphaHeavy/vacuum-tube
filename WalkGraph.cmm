#include <Cmm.h>

Serializze_encodeObject (W_ a, W_ st)
{
  W_ clos, info, type;
  clos = UNTAG(a);
  info = %GET_STD_INFO(clos);
  type = TO_W_(%INFO_TYPE(info));
  switch [INVALID_OBJECT .. N_CLOSURE_TYPES] (type)
  {
    default:
    {
      (W_ stx) = call %ENTRY_CODE(Serializze_pushTag_info) (clos, type, st);

      W_ ptrs;
      ptrs = TO_W_(%INFO_PTRS(info));
      jump Serializze_dumpPtrs (clos, 0, ptrs, stx);
    }
  }
}

Serializze_dumpPtrs (W_ clos, W_ p, W_ lim, W_ st)
{
  if (p < lim) {
    (W_ stx) = call %ENTRY_CODE(Serializze_yieldPtr_info) (clos, StgClosure_payload(clos, p), st);
    jump Serializze_dumpPtrs (clos, p + 1, lim, stx);
  } else {
    W_ info, nptrs;
    info = %GET_STD_INFO(clos);
    nptrs = TO_W_(%INFO_NPTRS(info));
    jump Serializze_dumpNPtrs (clos, p, lim + nptrs, st);
  }
}

Serializze_dumpNPtrs (W_ clos, W_ p, W_ lim, W_ st)
{
  if (p < lim) {
    (W_ stx) = call %ENTRY_CODE(Serializze_yieldNPtr_info) (clos, StgClosure_payload(clos, p), st);
    jump Serializze_dumpNPtrs (clos, p + 1, lim, stx);
  } else {
    jump %ENTRY_CODE(Serializze_popTag_info) (clos, st);
  }
}
