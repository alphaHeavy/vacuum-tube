#include <Cmm.h>

#define INPKG(a) vacuumzmtubezm0zi1zi0zi0_GHCziVacuumTubeziMorgue_ ## a

// VacuumWalker_getInfoTable :: Any -> State# s -> (# State# s, Addr# #)
VacuumWalker_getInfoTable (P_ a)
{
  return (%GET_STD_INFO(UNTAG(a)));
}

// VacuumWalker_dumpArray :: Addr# -> Word# -> Word# -> Word# -> EncodedState -> (# EncodedStates #)
VacuumWalker_dumpArray (P_ a, W_ clos, W_ info, W_ type, P_ st)
{
  // Serialize.pushArrWords :: Addr# -> Addr# -> ByteArray# -> EncodedState -> EncodedState
  (P_ stx) = call %ENTRY_CODE(INPKG(pushArrWords_info)) (INPKG(pushArrWords_closure), info, clos, st);

  // popState :: EncodedState -> EncodedState
  jump %ENTRY_CODE(INPKG(popState_info)) (INPKG(popState_closure), stx);
}

VacuumWalker_dumpClosure (P_ a, W_ clos, W_ info, W_ type, P_ st)
{
  W_ ptrs, nptrs;
  ptrs = TO_W_(%INFO_PTRS(info));
  nptrs = TO_W_(%INFO_NPTRS(info));

  // Serialize.pushClosure :: Word# -> Addr# -> Addr# -> EncodedState -> EncodedState
  (P_ stx) = call %ENTRY_CODE(INPKG(pushClosure_info)) (INPKG(pushClosure_closure), type, info, ptrs, nptrs, clos, st);

  W_ ptrs;
  ptrs = TO_W_(%INFO_PTRS(info));
  jump VacuumWalker_dumpPtrs (clos, 0, ptrs, stx);
}

// VacuumWalker_dumpIndirection :: Addr# -> Word# -> Word# -> Word# -> EncodedState -> (# EncodedStates #)
VacuumWalker_dumpIndirection (P_ a, W_ clos, W_ info, W_ type, P_ st)
{
  // Serialize.pushClosure :: Word# -> Addr# -> Addr# -> EncodedState -> EncodedState
  (P_ stx) = call %ENTRY_CODE(INPKG(pushClosure_info)) (INPKG(pushClosure_closure), type, info, 1, 0, clos, st);

  // // // Serialize.yieldArrWords :: Word# -> ByteArray# -> EncodedState -> EncodedState
  (P_ sty) = call %ENTRY_CODE(INPKG(yieldPtr_info)) (INPKG(yieldPtr_closure), 0, UNTAG(StgInd_indirectee(clos)), stx);

  // popState :: EncodedState -> EncodedState
  jump %ENTRY_CODE(INPKG(popState_info)) (INPKG(popState_closure), sty);
}

// VacuumWalker_dumpStatic :: Addr# -> Word# -> Word# -> Word# -> EncodedState -> (# EncodedStates #)
VacuumWalker_dumpStatic (P_ a, W_ clos, W_ info, W_ type, P_ st)
{
  // Serialize.pushStatic :: Word# -> Addr# -> Addr# -> EncodedState -> EncodedState
  (P_ stx) = call %ENTRY_CODE(INPKG(pushStatic_info)) (INPKG(pushStatic_closure), clos, st);

  // popState :: EncodedState -> EncodedState
  jump %ENTRY_CODE(INPKG(popState_info)) (INPKG(popState_closure), stx);
}

// Entry point in the Cmm side to walk the value graph pointed to from a
// while folding st over each value and their respective payloads
//
// VacuumWalker_encodeObject :: a -> EncodedState -> EncodedState
VacuumWalker_encodeObject (P_ a, P_ st)
{
  W_ clos, info, type;
  clos = UNTAG(a);
  if (clos == 0)
  {
    return (st);
  }

  info = %GET_STD_INFO(clos);
  type = TO_W_(%INFO_TYPE(info));

  switch [INVALID_OBJECT .. N_CLOSURE_TYPES] (type)
  {
    // specialization for ByteArray#
    case ARR_WORDS:
    {
      jump VacuumWalker_dumpArray (a, clos, info, type, st);
    }

    // @TODO: implement, frozen mutable arrays are ok
    case MUT_ARR_PTRS_FROZEN0, MUT_ARR_PTRS_FROZEN:
      goto FAIL;

    // but not after they're thawed
    case MUT_ARR_PTRS_CLEAN, MUT_ARR_PTRS_DIRTY:
      goto FAIL;

    // we've stumbled upon thread related bits
    case TSO, STACK, UPDATE_FRAME, CATCH_FRAME, UNDERFLOW_FRAME, STOP_FRAME:
      goto FAIL;

    // MVars
    case MUT_VAR_CLEAN, MUT_VAR_DIRTY:
      goto FAIL;

    // STM support
    case TREC_CHUNK, ATOMICALLY_FRAME, CATCH_RETRY_FRAME, CATCH_STM_FRAME:
      goto FAIL;

    // pointer types
    case WEAK, PRIM, MUT_PRIM:
      goto FAIL;

    // @TODO: unsure about these ... i'm guessing we need to handle them
    case RET_BCO, RET_SMALL, RET_BIG, RET_FUN, AP_STACK:
      goto FAIL;

    case BLACKHOLE, WHITEHOLE, IND, IND_PERM:
    {
      jump VacuumWalker_dumpIndirection (a, clos, info, type, st);
    }

    case IND_STATIC, CONSTR_STATIC, CONSTR_NOCAF_STATIC, FUN_STATIC, THUNK_STATIC:
    {
      jump VacuumWalker_dumpStatic (a, clos, info, type, st);
    }

    case CONSTR, CONSTR_1_0, CONSTR_0_1, CONSTR_2_0, CONSTR_1_1, CONSTR_0_2,
         FUN, FUN_1_0, FUN_0_1, FUN_2_0, FUN_1_1, FUN_0_2,
         THUNK, THUNK_1_0, THUNK_0_1, THUNK_2_0, THUNK_1_1, THUNK_0_2, THUNK_SELECTOR,
         AP, PAP:
    {
      jump VacuumWalker_dumpClosure (a, clos, info, type, st);
    }

    default:
    {
FAIL:
      // Serialize.unsupportedTag :: Word# -> Addr# -> Addr# -> EncodedState -> EncodedState
      jump %ENTRY_CODE(INPKG(unsupportedTag_info)) (INPKG(unsupportedTag_closure), type, info, clos, st);
    }
  }
}

// Call yieldPtr for each pointer payload then call dumpNPtrs for the rest
VacuumWalker_dumpPtrs (P_ clos, W_ p, W_ lim, P_ st)
{
  if (p < lim)
  {
    // yieldPtr :: Word# -> Addr# -> EncodedState -> EncodedState
    (P_ stx) = call %ENTRY_CODE(INPKG(yieldPtr_info)) (INPKG(yieldPtr_closure), p, UNTAG(StgClosure_payload(clos, p)), st);
    jump VacuumWalker_dumpPtrs (clos, p + 1, lim, stx);
  }
  else
  {
    W_ info, nptrs;
    info = %GET_STD_INFO(clos);
    nptrs = TO_W_(%INFO_NPTRS(info));
    jump VacuumWalker_dumpNPtrs (clos, p, lim + nptrs, st);
  }
}

// Call yieldNPtr for each non-pointer payload then pop the current value off
// the serialization stack, continuing on our walk
VacuumWalker_dumpNPtrs (P_ clos, W_ p, W_ lim, P_ st)
{
  if (p < lim)
  {
    // yieldNPtr :: Word# -> Word# -> EncodedState -> EncodedState
    (P_ stx) = call %ENTRY_CODE(INPKG(yieldNPtr_info)) (INPKG(yieldNPtr_closure), p, StgClosure_payload(clos, p), st);
    jump VacuumWalker_dumpNPtrs (clos, p + 1, lim, stx);
  }
  else
  {
    // popState :: EncodedState -> EncodedState
    jump %ENTRY_CODE(INPKG(popState_info)) (INPKG(popState_closure), st);
  }
}
